import InformationAlgebra.ShaferShenoy1991.Chapter2.P1HyperGraph


/-!
# HyperTree

This file introduces hypertrees.
-/

open HyperGraph


namespace HyperTree


variable
  {X : Type}
  [inst : DecidableEq X]


/-- The type of all proofs that a list of hyperedges can be decomposed into the sequential
addition of twigs.

Note that this type does not require that a twig involved in the construction sequence is disjoint
at each step. Although this is essential in the definition of a `HyperTree`, it has been excluded
from this type for accessibility reasons. That constraint is instead folded into the `HyperTree`
type itself, as a separate `List.Nodup` proof. -/
inductive ConsTwig : (HyperEdge X) â†’ (List (HyperEdge X)) â†’ Prop where
  /-- Any trivial hypergraph is a vacuously a hypertree.

  Note that a trivial hypergraph has one edge. -/
  | nil  : âˆ€ {a : HyperEdge X}, ConsTwig a []
  /-- By providing a proof that an edge `a` is a twig for the hypergraph comprised of the elements
  of a nonempty list `b::l` and a proof that `b::l` is itself constructed of twigs, we obtain a
  proof that the `a::b::l` is itself constructed by twigs. -/
  | cons : âˆ€ {a b : HyperEdge X} {l : List (HyperEdge X)}, (HyperGraph.Twig' (List.toHyperGraph b l) a) â†’ (ConsTwig b l) â†’ ConsTwig a (b::l)

attribute [simp] ConsTwig.nil
attribute [simp] ConsTwig.cons


/-- A `HyperTree` is a `HyperGraph`, together with an ordering of its edges such that each edge is
a twig for the hypergraph given by its downward section in the ordering.
-/
structure HyperTree (X : Type) [inst : DecidableEq X] where
  /-- The most recent edge added to the hypertree.

  Note that this is not the root. -/
  head : HyperEdge X
  /-- Other edges in the hypertree. -/
  tail : List (HyperEdge X)
  /-- No edges in a hypertree are repeated. -/
  nodup : (head::tail).Nodup
  /-- The hypertree is constructed by twigs. -/
  constwig : @ConsTwig X inst head tail


def HyperTree.toList (ğ’¯ : HyperTree X) : List (HyperEdge X) := ğ’¯.head::ğ’¯.tail


theorem toList_nonempty {ğ’¯ : HyperTree X} : ğ’¯.toList â‰  [] := List.getLast?_isSome.mp rfl


def HyperTree.toHyperGraph (ğ’¯ : HyperTree X) : HyperGraph X :=
  âŸ¨ğ’¯.toList.toFinset, List.toFinset_nonempty_iff (HyperTree.toList ğ’¯) |>.mpr toList_nonemptyâŸ©


instance : Coe (HyperTree X) (HyperGraph X) := âŸ¨HyperTree.toHyperGraphâŸ©


instance : Singleton (HyperEdge X) (HyperTree X) where
  singleton a := âŸ¨a, [], List.nodup_singleton a, ConsTwig.nilâŸ©


theorem coe_singleton {a : HyperEdge X} :  ({a} : HyperTree X) = ({a} : HyperGraph X) := rfl


instance : Membership (HyperEdge X) (HyperTree X) where
  mem a ğ’¯ := a âˆˆ ğ’¯.toList


@[simp]
theorem mem_cases {a : HyperEdge X} {ğ’¯ : HyperTree X} : (a âˆˆ ğ’¯) â†” a = ğ’¯.head âˆ¨ a âˆˆ ğ’¯.tail :=
  List.mem_cons


theorem mem_singleton {a b : HyperEdge X} : a âˆˆ ({b} : HyperTree X) â†” a = b := List.mem_singleton


theorem coe_mem {a : HyperEdge X} {ğ’¯ : HyperTree X} : a âˆˆ ğ’¯ â†” a âˆˆ (ğ’¯ : HyperGraph X) := by
  constructor <;> intro h
  Â· exact List.mem_toFinset.mpr h
  Â· exact List.mem_toFinset.mp h
  done


theorem nodup_tail (ğ’¯ : HyperTree X) : ğ’¯.tail.Nodup := List.nodup_cons.mp ğ’¯.nodup |>.right


theorem head_nin_tail (ğ’¯ : HyperTree X) : ğ’¯.head âˆ‰ ğ’¯.tail := List.nodup_cons.mp ğ’¯.nodup |>.left


theorem disjoint_edge_ne_head (ğ’¯ : HyperTree X) (a : HyperEdge X) (h : a âˆ‰ ğ’¯) : a â‰  ğ’¯.head := by
  simp only [mem_cases] at h
  intro hâ‚‚
  exact absurd (hâ‚‚) (not_or.mp h |>.left)
  done


theorem disjoint_edge_nin_tail (ğ’¯ : HyperTree X) (a : HyperEdge X) (h : a âˆ‰ ğ’¯) : a âˆ‰ ğ’¯.tail := by
  simp only [mem_cases] at h
  exact not_or.mp h |>.right
  done


theorem cons_list_nodup (ğ’¯ : HyperTree X) (a : HyperEdge X) (h : a âˆ‰ ğ’¯) : List.Nodup (a :: ğ’¯.head :: ğ’¯.tail) := by
    simp only [List.nodup_cons, Bool.not_eq_true, List.mem_cons]
    refine âŸ¨?_, head_nin_tail ğ’¯, nodup_tail ğ’¯âŸ©
    intro hâ‚‚
    apply Or.elim hâ‚‚
    Â· intro hâ‚ƒ
      exact absurd hâ‚ƒ (disjoint_edge_ne_head ğ’¯ a h)
    Â· intro hâ‚ƒ
      exact absurd hâ‚ƒ (disjoint_edge_nin_tail ğ’¯ a h)


theorem cons_twig (ğ’¯ : HyperTree X) (a : @HyperGraph.DisjointTwig X inst ğ’¯) : ConsTwig a (ğ’¯.head :: ğ’¯.tail) :=
  ConsTwig.cons a.property.right ğ’¯.constwig


-- TODO: Figure out why `HyperGraph.DisjointTwig` needs so much help.
/-- Attach a new twig to a hypertree, producing another hypertree. -/
def HyperTree.cons (ğ’¯ : HyperTree X) (a : @HyperGraph.DisjointTwig X inst ğ’¯) : HyperTree X := âŸ¨
    a,
    (ğ’¯.head::ğ’¯.tail),
    cons_list_nodup ğ’¯ a (Iff.not coe_mem |>.mpr a.property.left),
    cons_twig ğ’¯ a
  âŸ©


@[inherit_doc]
infixl:70 " ::â‚› "  => HyperTree.cons


theorem mem_cons {ğ’¯ : HyperTree X} {a : @HyperGraph.DisjointTwig X inst ğ’¯} {b : HyperEdge X} : b âˆˆ (ğ’¯::â‚›a) â†” b âˆˆ ğ’¯ âˆ¨ b = a := by
  constructor <;> intro h
  Â· cases h with
      | head _ => exact Or.inr rfl
      | tail _ hâ‚‚ => exact Or.inl hâ‚‚
  Â· dsimp only [Membership.mem] at *
    dsimp only [HyperTree.cons, HyperTree.toList] at *
    apply Or.elim h <;> intro hâ‚‚
    Â· exact List.Mem.tail _ hâ‚‚
    Â· rw [hâ‚‚]
      exact List.Mem.head _
  done


end HyperTree
